plugins {
    id 'java'
    id "com.avast.gradle.docker-compose" version "0.16.8"
}

apply plugin: "com.avast.gradle.docker-compose"

group 'Solution'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


task upgradeGradle(type: Wrapper) {
    gradleVersion = '7.4.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-bin.zip"
}

dockerCompose {
    useComposeFiles = ["docker-compose.yml"]
}

@CacheableRule
class JmeterRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext context) {
        context.details.allVariants {
            withDependencies {
                removeAll { it.group == "org.apache.jmeter" && it.name == "bom" }
            }
        }
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    testImplementation group: 'io.cucumber', name: 'cucumber-java8', version: '4.2.0'
    testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '4.2.0'
    testImplementation group: 'io.cucumber', name: 'cucumber-spring', version: '4.2.0'

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-edge-driver', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-ie-driver', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-opera-driver', version: '3.141.59'


    testImplementation group: 'org.springframework', name: 'spring-context', version: '5.1.3.RELEASE'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.1.3.RELEASE'

    /*Start Selenium Web*/
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.12.2'
    testImplementation "org.testcontainers:testcontainers:1.17.3"
    implementation group: 'com.epam.healenium', name: 'healenium-web', version: '3.2.5'
    implementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.2.1'
    /*End Selenium Web*/


    /*Appium*/
    implementation 'io.appium:java-client:8.1.1'
    /**/

    testImplementation 'us.abstracta.jmeter:jmeter-java-dsl:0.62.1'
    components {
        withModule("org.apache.jmeter:ApacheJMeter_core", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_java", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_http", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_functions", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_components", JmeterRule)
        withModule("org.apache.jmeter:ApacheJMeter_config", JmeterRule)
        withModule("org.apache.jmeter:jorphan", JmeterRule)
    }
}

task deleteCucumberReports(type: Delete) {
    dependsOn(clean)
    delete 'out', 'target', 'build'
}

/**
 * to run Cucumber tests with custom runTests task and with default browser, type:
 * 'runTests'
 *
 * to run Cucumber tests with custom runTests task and with chosen browser, type:
 * '-Dbrowser=chrome runTests'
 * '-Dbrowser=firefox runTests'
 * '-Dbrowser=chromeHeadless runTests'
 * '-Dbrowser=iexplorer runTests'
 *
 * to run Cucumber tests with chosen browser, type:
 * 'clean -Dbrowser=chrome test'
 * 'clean -Dbrowser=firefox test'
 * 'clean -Dbrowser=chromeHeadless test'
 * 'clean -Dbrowser=iexplorer test'
 */


task runTests (type: Test){
    dependsOn(deleteCucumberReports)
    systemProperty "browser", System.getProperty("browser")
}

